_ENV = Dora dora.Platformer

const W = 11520
const HW = W / 2
const H = 1080
const HH = H / 2
const Offset = 150
const PathOffset = Vec2 -50, -30
const MaxPath = 2
const DefaultZoom = 1.0

macro order = (value) ->
	global globals = globals or {}
	globals.layerId = if value then tonumber value
	else globals.layerId - 1
	globals.layerId

layerDefs =
	* look: "front", order: $order(100), ratio: Vec2(-0.2, 0)
	* look: "bushshadow", order: $order(-100), y: -70, z: 283
	* look: "road", order: $order, angleX: 45, scale: 1.22
	* look: "bush", order: $order, y: -70, z: 283
	* look: "light", order: $order, ratio: Vec2 1, 0
	* look: "cloud5", order: $order, ratio: Vec2(0.8, 0), z: 250, handle: => with @
		.speed = 1/10
		\play "cloud", true
	* look: "cloud4", order: $order, ratio: Vec2(0.82, 0), z: 250, handle: => with @
		.speed = 1/30
		\play "cloud", true
	* look: "cloud3", order: $order, ratio: Vec2(0.84, 0), z: 250, handle: => with @
		.speed = 1/50
		\play "cloud", true
	* look: "cloud2", order: $order, ratio: Vec2(0.86, 0), z: 250, handle: => with @
		.speed = 1/70
		\play "cloud", true
	* look: "fog", order: $order, ratio: Vec2(0.9, 0), y: -100, z: 250, handle: => with @
		.speed = 1/30
		\play "fogout"
	* look: "valley", order: $order, ratio: Vec2(0.8, -0.2), angleX: 45, scale: 1.2, x: -400
	* look: "mountain", order: $order, ratio: Vec2(0.86, 0), y: -200, z: 250
	* look: "cloud1", order: $order, ratio: Vec2(0.8, 0), z: 250, handle: => with @
		.speed = 1/90
		\play "cloud", true
	* look: "back", order: $order, ratio: Vec2(1, 0)

addLayer = (world, def) ->
	layer = with Spine "outerHeaven"
		.look = def.look
		.order = def.order
		.scaleX = def.scale or 1.0
		.scaleY = def.scale or 1.0
		.x = def.x or 0
		.y = def.y or 0
		.z = def.z or 0
		.angleX = def.angleX or 0
		.angleY = def.angleY or 0
	if def.ratio
		world\setLayerRatio def.order, def.ratio
	world\addChild layer
	def.handle layer if def.handle
	layer

export default Class
	width: property -> W
	offset: property -> Offset

	addShadowTo: (unit) =>
		with Sprite "Image/shadow.png"
			.order = -1
			\schedule -> .y = @offset - unit.y
			\addTo unit

	zoom: property => @_zoom,
		(value) =>
			@_zoom = value
			@updateZoom!

	updateZoom: =>
		:scale = View
		:width, :height = View.size * Vec2 scale, scale
		zoom = @_zoom / DefaultZoom
		zoom = math.max width / W / zoom, height / H / zoom
		realWidth = math.min W / zoom, W
		realHeight = math.min H / zoom, H
		zoom = math.max W / realWidth, H / realHeight
		@camera.zoom = zoom / scale

	__partial: (_) ->
		with PlatformWorld!
			with .camera
				.boundary = Rect 0, 0, W, H
				.followRatio = Vec2 0.03, 0.03
			.tag = "OuterHeaven"

	__init: =>
		for i = -MaxPath, MaxPath
			@setLayerOffset i, PathOffset * i
			@getLayer(i).z = -50 * i

		for def in *layerDefs
			addLayer @, def

		terrainDef = with BodyDef!
			.type = "Static"
			\attachPolygon Vec2(HW, 0), W, 10, 0, 1, 1, 0
			\attachPolygon Vec2(HW, H), W, 10, 0, 1, 1, 0
			\attachPolygon Vec2(0, HH), 10, H, 0, 1, 1, 0
			\attachPolygon Vec2(W, HH), 10, H, 0, 1, 1, 0
			.position = Vec2 0, Offset

		with Body terrainDef, @, Vec2.zero
			.group = Data.groupTerrain
			\addTo @

		@_zoom = 1.0
		@gslot "AppSizeChanged", -> @updateZoom!
		@updateZoom!

	loadAsync: (_) -> Cache\loadAsync "spine:outerHeaven"
